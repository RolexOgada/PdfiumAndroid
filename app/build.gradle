plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace "com.shockwave.pdfium"
    compileSdk 36

    defaultConfig {
        minSdk 21
        targetSdk 36
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    sourceSets {
        main {
            jni.srcDirs = []
            jniLibs.srcDir "src/main/jniLibs"
        }
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout

    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}

// Maven Publish configuration
afterEvaluate {
    publishing {
        publications {
            create("release", MavenPublication) {
                // Use the 'release' library variant correctly
                from(components.findByName("release"))

                groupId = "com.github.RolexOgada"
                artifactId = "pdfium-android"
                version = "1.0.0"

                pom {
                    name.set("PdfiumAndroid Library")
                    description.set("Forked library for rendering PDFs on Android")
                    url.set("https://github.com/RolexOgada/PdfiumAndroid")
                    licenses {
                        license {
                            name.set("MIT License")
                            url.set("https://opensource.org/licenses/MIT")
                        }
                    }
                    developers {
                        developer {
                            id.set("rolexogada")
                            name.set("Rolex Ogada")
                            email.set("rolexogada@gmail.com")
                        }
                    }
                    scm {
                        connection.set("scm:git:git://github.com/RolexOgada/PdfiumAndroid.git")
                        developerConnection.set("scm:git:ssh://git@github.com/RolexOgada/PdfiumAndroid.git")
                        url.set("https://github.com/RolexOgada/PdfiumAndroid")
                    }
                }
            }
        }
    }
}

// Ensure assembleRelease is run during JitPack build
tasks.named("publish") {
    dependsOn("assembleRelease")
}
